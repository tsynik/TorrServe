import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

def freeKeystorePropertiesFile = rootProject.file("free.property")
def freeProperties = new Properties()
freeProperties.load(new FileInputStream(freeKeystorePropertiesFile))

android {
    compileSdkVersion 28
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    defaultConfig {
        applicationId "ru.yourok.torrserve"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 75
        versionName "1.1.75"
    }
    signingConfigs {
        release {
            keyAlias freeProperties['keyAlias']
            keyPassword freeProperties['keyPassword']
            storeFile rootProject.file(freeProperties['storeFile'])
            storePassword freeProperties['storePassword']
        }
        versionCode 76
        versionName "1.1.76"
    }
    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "TorrServe_${versionName}.apk"
                }
            }
        }
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    // Support
    // implementation 'com.android.support:appcompat-v7:28.0.0'
    // implementation 'com.android.support:design:28.0.0'
    // implementation 'com.android.support:cardview-v7:28.0.0'
    // AndroidX
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.0.0'

    // ExoPlayer v2
    implementation "com.google.android.exoplayer:exoplayer-core:2.10.4"
    implementation "com.google.android.exoplayer:exoplayer-ui:2.10.4"

    // ExoPlayer v2 MediaSession extension
    implementation "com.google.android.exoplayer:extension-mediasession:2.10.4"

    implementation 'com.mxn.soul:flowingdrawer-core:2.0.0'
    implementation 'com.nineoldandroids:library:2.4.0'

    implementation 'cz.msebera.android:httpclient:4.4.1.2'

    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.google.code.gson:gson:2.8.5'

    implementation 'com.google.firebase:firebase-core:16.0.9'
}

repositories {
    jcenter()
}

class WriteVersion implements Plugin<Project> {
    void apply(Project project) {
        project.task("WriteVersion") {
            project.gradle.projectsEvaluated {
                println "Write: release.json"

                def appId = "$project.android.defaultConfig.applicationId"
                def version = "$project.android.defaultConfig.versionName"

                println appId
                println version

                def stringsFile = new File("release.json")
                def json = """{
 "Name": "${appId}",
 "Version": "${version}",
 "BuildDate": "${new SimpleDateFormat("dd.MM.yyyy").format(new Date())}",
 "Link": "https://github.com/YouROK/TorrServe/releases/download/${version}/TorrServe_${version}.apk"
}
"""
                stringsFile.write(json)
            }
        }
    }
}

apply plugin: WriteVersion
apply plugin: 'com.google.gms.google-services'